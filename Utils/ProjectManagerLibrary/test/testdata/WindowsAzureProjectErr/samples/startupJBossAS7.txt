@REM *** Sample startup script containing the steps for starting JBoss Application Server 7 and deploying a WAR file. 
@REM *** (Last tested with JBoss Application Server 7.0.2)

@REM To use the sample, follow these steps:
@REM *** 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
@REM *** 2) Place a JRE distribution as jre.zip under approot
@REM *** 3) Place a JBoss Application Server 7 distribution as jboss7.zip under approot in your project
@REM *** 3.1) If you want to download the server into Azure directly from a URL instead, then
@REM          uncomment the next line and modify the URL as appropriate:
@REM cscript "util\download.vbs" "http://download.jboss.org/jbossas/7.0/jboss-as-7.0.2.Final/jboss-as-7.0.2.Final.zip" "jboss7.zip"

@REM *** 4) Update SERVER_DIR_NAME below as appropriate:
@REM *** (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=jboss-as-7.0.2.Final

@REM *** 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
@REM *** (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war

@REM *** 6) To enable the management console, add an input endpoint 9990/9990 to your Windows Azure configuration
		
			
@REM **************************************************************			
@REM *** Do not make changes below unless you know what you are doing.
rd "\%ROLENAME%"
mklink /D "\%ROLENAME%" "%ROLEROOT%\approot"
cd /d "\%ROLENAME%"
cscript util\unzip.vbs jre.zip "%CD%"
cscript util\unzip.vbs jboss7.zip "%CD%"
copy "%WAR_NAME%" "%SERVER_DIR_NAME%\standalone\deployments"
cd /d "%SERVER_DIR_NAME%\bin"
set JRE_HOME=\%ROLENAME%\jre
set JAVA_HOME=\%ROLENAME%\jre
set JBOSS_Home=\%ROLENAME%\%SERVER_DIR_NAME%
start standalone.bat -b 0.0.0.0 -bmanagement 0.0.0.0

@ECHO OFF
if %ERRORLEVEL%==0 exit %ERRORLEVEL%
choice /d y /t 5 /c Y /N /M "*** Windows Azure startup failed - exiting..."
exit %ERRORLEVEL%