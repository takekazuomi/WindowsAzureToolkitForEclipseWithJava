@REM *** Sample startup script containing the steps for starting Jetty 7 and deploying a WAR file. 
@REM *** (Last tested with Jetty 7.4.5)

@REM To use the sample, follow these steps:
@REM *** 1) Copy all this content into approot/startup.cmd in the role folder, close this file, and edit the copy
@REM *** 2) Place a JRE distribution as jre.zip under approot
@REM *** 3) Place a Jetty 7.x distribution as jetty7.zip under approot in your project
@REM *** 3.1) If you want to download the server into Azure directly from a URL instead, then
@REM          uncomment the next line and modify the URL as appropriate:
@REM cscript "util\download.vbs" "http://download.eclipse.org/jetty/7.4.5.v20110725/dist/jetty-distribution-7.4.5.v20110725.zip" "jetty7.zip"

@REM *** 4) Update SERVER_DIR_NAME below as appropriate:
@REM *** (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET SERVER_DIR_NAME=jetty-distribution-7.4.5.v20110725

@REM *** 5) To deploy your own WAR file, place it in approot and update WAR_NAME below:
@REM *** (IMPORTANT: There must be no trailing nor leading whitespace around the setting)

SET WAR_NAME=HelloWorld.war

			
@REM **************************************************************			
@REM *** Do not make changes below unless you know what you're doing.
rd "\%ROLENAME%"
mklink /D "\%ROLENAME%" "%ROLEROOT%\approot"
cd /d "\%ROLENAME%"
cscript util\unzip.vbs jre.zip "%CD%"
cscript util\unzip.vbs jetty7.zip "%CD%"
copy %WAR_NAME% "%SERVER_DIR_NAME%\webapps\%WAR_NAME%"
cd "%SERVER_DIR_NAME%"
start \%ROLENAME%\jre\bin\java.exe -jar start.jar

@ECHO OFF
if %ERRORLEVEL%==0 exit %ERRORLEVEL%
choice /d y /t 5 /c Y /N /M "*** Windows Azure startup failed - exiting..."
exit %ERRORLEVEL%